plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def gitCommitCount
try {
    gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
} catch (ignored) {
    gitCommitCount = 1
}

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'
    namespace 'io.github.vvb2060.keyattestation'
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode gitCommitCount
        versionName '1.3.3'
        resConfigs "en", "zh-rCN", "zh-rTW"
    }

    signingConfigs {
        if (properties.getProperty("storeFile") != null) {
            sign {
                storeFile file(properties.getProperty("storeFile"))
                storePassword properties.getProperty("storePassword")
                keyAlias properties.getProperty("keyAlias")
                keyPassword properties.getProperty("keyPassword")
            }
        }
    }
    buildTypes {
        debug {
            if (properties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            if (properties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.sign
            } else {
                signingConfig signingConfigs.debug
            }
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        resources {
            excludes += '**'
        }
    }

    lintOptions.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation 'org.bouncycastle:bcprov-jdk18on:1.71'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'co.nstant.in:cbor:0.9'

    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.5.0'
    implementation 'dev.rikka.rikkax.core:core-ktx:1.4.1'
    //noinspection GradleDependency
    implementation 'dev.rikka.rikkax.material:material:1.6.6'
    implementation 'dev.rikka.rikkax.html:html-ktx:1.1.2'
    implementation 'dev.rikka.rikkax.recyclerview:recyclerview-adapter:1.3.0'
    implementation 'dev.rikka.rikkax.widget:borderview:1.1.0'

    implementation 'com.google.android.material:material:1.6.1'

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    def lifecycleVersion = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
}
