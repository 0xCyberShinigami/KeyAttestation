apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

def gitCommitCount
try {
    gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
} catch(ignored) {
    gitCommitCount = 1
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'io.github.vvb2060.keyattestation'
        minSdkVersion 24
        targetSdkVersion 30
        versionCode gitCommitCount
        versionName '1.2.0'
        resConfigs "en", "zh-rCN", "zh-rTW"
    }

    buildTypes {
        debug {
            manifestPlaceholders = [crashReportingEnabled: "false"]
        }
        release {
            manifestPlaceholders = [crashReportingEnabled: "true"]
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles 'proguard-rules.pro'
        }
        xposed {
            initWith debug
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig = false
        viewBinding = true
    }
    packagingOptions {
        exclude 'META-INF/**'
        exclude 'kotlin/**'
        exclude 'okhttp3/**'
        exclude 'org/**'
        exclude '**.properties'
        exclude '**.bin'
    }
    lintOptions.checkReleaseBuilds false
    dependenciesInfo.includeInApk false
}

repositories {
    maven { url 'https://dl.bintray.com/rikkaw/Libraries' }
}

configurations.all {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {
    def kotlin_coroutines_version = '1.4.2'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    xposedCompileOnly 'de.robv.android.xposed:api:82'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'rikka.appcompat:appcompat:1.2.0-rc01'
    implementation 'rikka.core:core-ktx:1.3.0'
    implementation 'rikka.material:material:1.6.0'
    implementation 'rikka.html:html:1.1.2'
    implementation 'rikka.recyclerview:recyclerview-utils:1.2.0'
    implementation 'rikka.widget:borderview:1.0.0'

    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.core:core-ktx:1.5.0-beta01'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    def lifecycleVersion = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    implementation 'com.google.firebase:firebase-analytics:18.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
}

android.applicationVariants.all { variant ->
    if (variant.buildType.minifyEnabled) {
        copy {
            from 'src/main/res/values'
            into "build/generated/res/google-services/${variant.getName()}/values"
            include 'google-services.xml'
            rename { String s ->
                s.replace("google-services.xml", "values.xml")
            }
        }
    }
}